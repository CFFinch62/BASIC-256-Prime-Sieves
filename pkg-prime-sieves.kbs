# PRIME SIEVE PACKAGE

# Sieve of Atkin
function primes_SoA(limit)
    Dim marked(limit + 1) fill false
    for x = 1 to (sqrt(limit) + 1)
        for y = 1 to (sqrt(limit) + 1)
            n = (4 * (x*x)) + (y*y)
            if (n <= limit) and ((n mod 12 = 1) or (n mod 12 = 5)) then 
                marked[n] = not marked[n]
            end if
            n = (3 * (x*x)) + (y*y)
            if (n <= limit) and (n mod 12 = 7) then
                marked[n] = not marked[n]
            end if
            n = (3 * (x*x)) - (y*y)
            if (x > y) and (n <= limit) and (n mod 12 = 11) then
                marked[n] = not marked[n]
            end if
        next y
     next x   
     for n = 5 to (sqrt(limit) + 1)
         if marked[n] = true then
             k = 1
             while k * (n*n) <= limit
                 marked[k * (n*n)] = false
                 k = k + 1
             end while
         end if
     next n       
    cnt = 0 
    for i = 4 to limit
        if marked[i] = true then
			cnt = cnt + 1
    	end if
    next i           
    Dim primes(cnt + 2)
    primes[0] = 2
    primes[1] = 3
    c = 2
    for i = 4 to limit
        if marked[i] = true then
            primes[c] = i
            c = c + 1
        end if   
    next i       
    return primes    
end function

# Sieve of Eratosthenes
function primes_SoE(n)
    Dim marked(n+1) fill false
    for i = 3 to n step 2
    	marked[i] = true
    next i
    for i = 3 to n step 2
    	if marked[i] = true then
    		for j = i*i to n step i
    			marked[j] = false
    		next j
    	end if
    next i
    cnt = 0 
    for i = 4 to n
        if marked[i] = true then
			cnt = cnt + 1
    	end if
    next i     
    Dim primes(cnt + 2)
    primes[0] = 2
    primes[1] = 3
    c = 2
    for i = 4 to n
        if marked[i] = true then
            primes[c] = i
            c = c + 1
        end if   
    next i
    return primes
end function

# Sieve of Sundaram
function primes_SoS(n)
    Dim nNew = int((n - 1) / 2)
    Dim marked(nNew + 1) fill false 
    for i = 1 to nNew + 1
        j = i
        while((i + j + 2 * i * j) <= nNew)
            marked[i + j + 2 * i * j] = true
            j = j + 1
        end while
    next i
    cnt = 0 
    for i = 1 to nNew
        if marked[i] = false then
			cnt = cnt + 1
    	end if
    next i       
    Dim primes(cnt + 1)
    primes[0] = 2
    c = 1
    for i = 1 to nNew
        if marked[i] = false then
            primes[c] = 2 * i + 1
            c = c + 1
        end if   
    next i
    return primes
end function

# print prime list
subroutine primes_print(list)
	print "The list of primes is:"
	for p = 0 to (length(list) - 1)
    	print list[p]; " ";
	next p
	print " "
end subroutine